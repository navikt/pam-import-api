buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
    }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node' // gradle-node-plugin

node {
    /* gradle-node-plugin configuration
       https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md

       Task name pattern:
       ./gradlew npm_<command> Executes an NPM command.
    */

    // Version of node to use.
    version = '13.11.0'

    // Version of npm to use.
    npmVersion = '6.13.7'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

}


npm_run_build {

    // make sure the build task is executed only when appropriate files change
    inputs.files fileTree('src')
    // 'node_modules' appeared not reliable for dependency change detection (the task was rerun without changes)
    // though 'package.json' and 'package-lock.json' should be enough anyway
    inputs.file 'package.json'
    inputs.file 'package-lock.json'

}

// pack output of the build into JAR file
task packageNpmApp(type: Zip) {
    dependsOn npm_install
    dependsOn npm_run_build
    baseName 'frontend'
    extension 'jar'
    destinationDir file("${projectDir}/build")
    from('dist') {
        into 'frontend'
    }
}

// declare a dedicated scope for publishing the packaged JAR
configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

// expose the artifact created by the packaging task
artifacts {
    npmResources(packageNpmApp.archivePath) {
        builtBy packageNpmApp
        type 'jar'
    }
}

assemble.dependsOn packageNpmApp

clean {
    delete packageNpmApp.archivePath
}
